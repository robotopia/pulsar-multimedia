# Assumes "clock", "alpha", "zeta", and "phase" are all defined

unset colorbox
unset key
unset border
unset tics

set view equal xyz
set hidden3d front

set parametric
set isosamples 33

set urange [0:pi]
set vrange [0:2*pi]

set xrange [-3.0:3.0]
set yrange [-3.0:3.0]
set zrange [-3.0:3.0]

# Max distance from origin for dipole field lines
R  = 4.0
rp = 0.6 # pulsar radius

# Setup random variable for plotting currents
ne = 1000 # number of "electrons"
maxdist = 3 # stellar radii
speed   = 1.0 # rp per video length
set print $random
do for [i=1:ne] {
    starttime = rand(0) * ((1-maxdist) * rp / speed - 1) + 1
    magtheta  = asin(sqrt(rp/R))*rand(0)
    magphi    = 2*pi*rand(0)
    print sprintf("%f %f %f", starttime, magtheta, magphi)
}

# Rotation to get from rotation to magnetic frame:
magx(x,y,z) = cos(phase)*cos(alpha)*x - sin(phase)*y + cos(phase)*sin(alpha)*z
magy(x,y,z) = sin(phase)*cos(alpha)*x + cos(phase)*y + sin(phase)*sin(alpha)*z
magz(x,y,z) =           -sin(alpha)*x                +            cos(alpha)*z

# Colat/Long coords --> Cartesian
xx(u, v) = sin(u) * cos(v)
yy(u, v) = sin(u) * sin(v)
zz(u, v) = cos(u)

# Formulae for field lines
r(th) = R * sin(th)**2         # (th = colat)
rx(th,phi,r) = r*xx(th,phi) # (0 < u=th < pi; phi = azimuth)
ry(th,phi,r) = r*yy(th,phi) #              "
rz(th,phi,r) = r*zz(th,phi) #              "

# Formula for outflowing particles radial dependence on time
th(r,th0,r0) = asin(sqrt(r/r0 * sin(th0)**2))
pr(now,starttime) = (now-starttime >= 0 && now-starttime <= 1) ? \
                       rp + maxdist*speed*(now-starttime) : 1/0

sigma(phi) = cmod(atan2(sin(zeta)*sin(phi), sin(alpha)*cos(zeta) - cos(alpha)*sin(zeta)*cos(phi)) + pi,2*pi)

splot rp*magx(xx(u,v),yy(u,v),zz(u,v)), \
      rp*magy(xx(u,v),yy(u,v),zz(u,v)), \
      rp*magz(xx(u,v),yy(u,v),zz(u,v)), \
      for [lineno=0:7] \
          magx(rx(u,lineno*pi/4,r(u)),ry(u,lineno*pi/4,r(u)),rz(u,lineno*pi/4,r(u))), \
          magy(rx(u,lineno*pi/4,r(u)),ry(u,lineno*pi/4,r(u)),rz(u,lineno*pi/4,r(u))), \
          magz(rx(u,lineno*pi/4,r(u)),ry(u,lineno*pi/4,r(u)),rz(u,lineno*pi/4,r(u))) lt 2, \
      $random using \
          (magx(rx(th(pr(clock,$1),$2,rp),$3,pr(clock,$1)), \
                ry(th(pr(clock,$1),$2,rp),$3,pr(clock,$1)), \
                rz(th(pr(clock,$1),$2,rp),$3,pr(clock,$1)))): \
          (magy(rx(th(pr(clock,$1),$2,rp),$3,pr(clock,$1)), \
                ry(th(pr(clock,$1),$2,rp),$3,pr(clock,$1)), \
                rz(th(pr(clock,$1),$2,rp),$3,pr(clock,$1)))): \
          (magz(rx(th(pr(clock,$1),$2,rp),$3,pr(clock,$1)), \
                ry(th(pr(clock,$1),$2,rp),$3,pr(clock,$1)), \
                rz(th(pr(clock,$1),$2,rp),$3,pr(clock,$1)))) \
           w p pt 7 ps 0.2 lc rgb "yellow"
