set terminal pngcairo enhanced size 1200,600 font ",20"

set object 1 rectangle from screen 0,0 to screen 1,1 fc rgb "black"

singlepulse  = "singlepulse.dat"
samplepoints = "singlepulse-sampled.dat"

unset xtics
unset ytics

unset key
unset border

N = system("cat ".singlepulse." | wc -l")
n = system("cat ".samplepoints." | wc -l")

set view equal xyz

set parametric
set isosamples 17, 17
set samples 361, 361
set urange [0:1] # Ranging over latitude
set vrange [0:1] # Ranging over longitude

set xrange [-1.5:1.5]
set yrange [-1.5:1.5]
set zrange [-1:2]
set view 70.0, 0.0, 1.0, 1.0
set xyplane 0

alpha = 20*pi/180.0
zeta  = 35*pi/180.0

load 'geometry.gpi'
heights = "1.2 1.4 1.6 1.8 2.0 2.2"
colours = "magenta blue green yellow orange red"

# First plot: 3D wireframe model of pulsar and emission sites at single height
do for [i=0:n] {

    outfile = sprintf("3D-sampledpulse_%03d.png", i)
    set output outfile
    emissionheight = word(heights, 1)
    emissionpoints = sprintf("< head -%d %s", i, samplepoints)

    splot magx(xx(sph_coord_th(u),sph_coord_ph(v),1), \
               yy(sph_coord_th(u),sph_coord_ph(v),1), \
               zz(sph_coord_th(u),sph_coord_ph(v),1), 0),  \
          magy(xx(sph_coord_th(u),sph_coord_ph(v),1), \
               yy(sph_coord_th(u),sph_coord_ph(v),1), \
               zz(sph_coord_th(u),sph_coord_ph(v),1), 0),  \
          magz(xx(sph_coord_th(u),sph_coord_ph(v),1), \
               yy(sph_coord_th(u),sph_coord_ph(v),1), \
               zz(sph_coord_th(u),sph_coord_ph(v),1), 0)  \
          lc rgb "white", \
          emissionpoints u (LoSx($1*pi/180.0+pi/2,emissionheight)):\
                           (LoSy($1*pi/180.0+pi/2,emissionheight)):\
                           (LoSz($1*pi/180.0+pi/2,emissionheight)) \
          pt 7 ps 1.5 lc rgb word(colours, 1)

}

# Second plot: 3D wireframe model of pulsar and emission sites at multiple heights
do for [i=1:words(heights)] {
    outfile = sprintf("3D-heights_%03d.png", i-1)
    set output outfile
    emissionheight = word(heights, i)

    splot magx(xx(sph_coord_th(u),sph_coord_ph(v),1), \
               yy(sph_coord_th(u),sph_coord_ph(v),1), \
               zz(sph_coord_th(u),sph_coord_ph(v),1), 0),  \
          magy(xx(sph_coord_th(u),sph_coord_ph(v),1), \
               yy(sph_coord_th(u),sph_coord_ph(v),1), \
               zz(sph_coord_th(u),sph_coord_ph(v),1), 0),  \
          magz(xx(sph_coord_th(u),sph_coord_ph(v),1), \
               yy(sph_coord_th(u),sph_coord_ph(v),1), \
               zz(sph_coord_th(u),sph_coord_ph(v),1), 0)  \
          lc rgb "white", \
          for [j=1:i] samplepoints u (LoSx($1*pi/180.0,word(heights, j))):\
                                     (LoSy($1*pi/180.0,word(heights, j))):\
                                     (LoSz($1*pi/180.0,word(heights, j))) \
          pt 7 ps 1.5 lc rgb word(colours, j)

}

# Third plot: Timeseries

set border lc rgb "white"
set xlabel "1 pulsar rotation" tc rgb "white"
set xrange [*:*] noextend
set yrange [*:*] noextend

do for [i=0:n] {
    outfile = sprintf("2D-sampledpulse_%03d.png", i)
    set output outfile
    plot singlepulse u ($0*360.0/N):1 w l lc rgb "white", \
         "< head -".i." ".samplepoints w p pt 7 ps 2 lc rgb word(colours, 1)
}

set border lc rgb "white"
set xlabel "1 pulsar rotation" tc rgb "white"
set ylabel "Observing Frequency" tc rgb "white"
set xrange [*:*]
set yrange [1:words(heights)]
set zrange [*:*]
set view noequal
set view 73, 309, 1, 1
do for [i=1:words(heights)] {
    outfile = sprintf("2D-heights_%03d.png", i-1)
    set output outfile
    splot for [j=1:i] singlepulse  u ($0*360.0/N):(j):($1*(1-0.1*j)) w l lc rgb "white", \
          for [j=1:i] samplepoints u 1:(j):($2*(1-0.1*j)) pt 7 ps 2 lc rgb word(colours, j)
}
