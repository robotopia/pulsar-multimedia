# Assumes "alpha", "zeta", "plotactivefieldline", and "phase" are all defined

unset colorbox
unset key
unset border
unset tics

set view equal xyz

set parametric
set samples 361
set isosamples 16

set urange [0:pi]
set vrange [0:2*pi]

set xrange [-3.0:3.0]
set yrange [-3.0:3.0]
set zrange [-3.0:3.0]

# Rotation to get from rotation to magnetic frame:
magx(x,y,z) = cos(phase)*cos(alpha)*x - sin(phase)*y + cos(phase)*sin(alpha)*z
magy(x,y,z) = sin(phase)*cos(alpha)*x + cos(phase)*y + sin(phase)*sin(alpha)*z
magz(x,y,z) =           -sin(alpha)*x                +            cos(alpha)*z

# Colat/Long coords --> Cartesian
xx(u, v) = sin(u) * cos(v)
yy(u, v) = sin(u) * sin(v)
zz(u, v) = cos(u)

# Max distance from origin for dipole field lines
R = 2 # Assumes pulsar has radius 1

# Formulae for field lines
r(th) = R * sin(th)**2       # (th = colat)
rx(th,phi) = r(th)*xx(th,phi)  # (0 < u=th < pi; phi = azimuth)
ry(th,phi) = r(th)*yy(th,phi)  #              "
rz(th,phi) = r(th)*zz(th,phi)  #              "

splot magx(xx(u,v),yy(u,v),zz(u,v)), \
      magy(xx(u,v),yy(u,v),zz(u,v)), \
      magz(xx(u,v),yy(u,v),zz(u,v)), \
      for [phi=pi/4:pi/4:2*pi] \
          magx(rx(u,phi),ry(u,phi),rz(u,phi)), \
          magy(rx(u,phi),ry(u,phi),rz(u,phi)), \
          magz(rx(u,phi),ry(u,phi),rz(u,phi))
