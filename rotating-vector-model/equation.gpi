# Assumes "alpha", "zeta", and "clock" are all defined
# (0 < alpha < pi)
# (0 <  zeta < pi)
# (0 < clock < 1 )

ymargin = 0.7
xmargin = 0.5

phase = clock*2*pi
set angle radians

# Equivalent to a palette map from transparent to yellow
transyellow(t) = (floor(255*(1-t)) << 24) + (255 << 16) + (255 << 8) + 0

# Some general modulus functions
mod(x,m) = x - floor(1.0*x/m)*m
cmod(x,m) = mod(x-0.00001+m/2.0,m)-m/2.0 # The "-0.00001" is to ensure m/2 < x <= m/2

set multiplot

    # Set global background
    set object 1 rectangle from screen 0,0 to screen 1,1 fc rgb "black" behind
    set lt 1 lc rgb "white"
    set lt 2 lc rgb "#77FF00FF" # Semitransparent magenta

    # Top plot = position angle
    set tmargin at screen 0.98
    set bmargin at screen ymargin + 0.05
    set lmargin at screen 0.10 # Leave room for axis labels etc
    set rmargin at screen 0.98

    load 'eqpositionangle.gpi'

    unset object 1

    # Bottom left = side view
    set tmargin at screen ymargin
    set bmargin at screen 0.0
    set lmargin at screen 0.0
    set rmargin at screen xmargin

    arrowlength = 2.0
    set arrow 1 from 0,0,0 to 0, \
                              0, \
                              arrowlength \
                              lc rgb "white" # Rotation axis
    set arrow 2 from 0,0,0 to arrowlength*sin(zeta), \
                              0, \
                              arrowlength*cos(zeta) \
                              lc rgb "white" # Line of sight
    set arrow 3 from 0,0,0 to arrowlength*sin(alpha)*cos(phase), \
                              arrowlength*sin(alpha)*sin(phase), \
                              arrowlength*cos(alpha) \
                              lc rgb "white"

    plotactivefieldline = "no"

    load 'sideview.gpi'

    unset arrow 1
    unset arrow 2
    unset arrow 3

    # Bottom right = "draw" equations
    set tmargin at screen ymargin
    set bmargin at screen 0.0
    set lmargin at screen xmargin
    set rmargin at screen 1.0

    set xrange [0:1]
    set yrange [-1:1]
    unset xlabel
    unset ylabel

    eq1x = 0.2
    eq1y =  0.3
    eq1r = 0.9
    eq1rmid = (eq1x+eq1r)/2.0

    eq2x = 0.4
    eq2y = -0.3
    eq2r = 0.6
    eq2rmid = (eq2x+eq2r)/2.0

    fracheight = 0.08

    # Equations in Gnuplot...!  (Ugh!)
    set label 1 "tan(P.A.) = " at eq1x,eq1y right tc rgb "white" font ",12"
    set label 2 "sin(α) sin(ζ)" at eq1rmid,eq1y+fracheight center tc rgb "white" font ",12"
    set label 3 "cos(α) sin(ζ) - sin(α) cos(ζ) cos(φ)" at eq1rmid,eq1y-fracheight center tc rgb "white" font ",12"

    set label 4 "Max" at eq2x-0.12,eq2y+0.04 center tc rgb "white" font ",12"
    set label 5 "slope" at eq2x-0.12,eq2y-0.04 center tc rgb "white" font ",12"
    set label 6 "= " at eq2x,eq2y right tc rgb "white" font ",12"
    set label 7 "sin(α)" at eq2rmid,eq2y+fracheight center tc rgb "white" font ",12"
    set label 8 "cos(α+ζ)" at eq2rmid,eq2y-fracheight center tc rgb "white" font ",12"

    # Plot the fraction lines
    unset parametric
    plot (x > eq1x && x < eq1r ? eq1y : 1/0) lw 2 lc rgb "white", \
         (x > eq2x && x < eq2r ? eq2y : 1/0) lw 2 lc rgb "white"

unset multiplot
