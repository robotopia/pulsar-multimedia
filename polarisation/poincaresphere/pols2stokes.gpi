Sx(phi,chi) = cos(2*chi)*cos(2*phi)
Sy(phi,chi) = cos(2*chi)*sin(2*phi)
Sz(phi,chi) = sin(2*chi)

set multiplot

    # Plot poincare sphere in top right corner
    set object 1 rectangle from screen 0,0 to screen 1,1 behind fc rgb "black"

    unset border
    unset key
    unset tics

    set parametric
    set trange [0:2*pi]

    set xrange [-1.5:1.5]
    set yrange [-1.5:1.5]
    set zrange [-1.2:1.8]
    set xyplane 0

    set bmargin at screen 0.1
    set lmargin at screen 0.6
    set tmargin at screen 0.5
    set rmargin at screen 1.0

    set urange [0:pi]
    set vrange [-pi/2:pi/2]

    set isosamples 33

    set view 60,110,2,1
    unset colorbox
    set hidden3d front
    set view equal xyz

    set arrow 1 from 0,0,0 to 2.4,0,0 lc rgb "yellow"
    set arrow 2 from 0,0,0 to 0,1.5,0 lc rgb "green"
    set arrow 3 from 0,0,0 to 0,0,1.7 lc rgb "cyan"

    set arrow 4 from 0,0.5,1.7 to 0,0,1  front        lw 2 lc rgb "red"
    set arrow 5 from 0,0,-1.4  to 0,0,-1.16 front nohead lw 2 lc rgb "red"

    set label 1 "Right Circular" at 0,0.1,1.7 left   tc rgb "white"
    set label 2  "Left Circular" at 0,0,-1.5  center tc rgb "white"
    set label 3         "Linear" at 0,1.4,0.2 center tc rgb "white"

    set label 4 "Q " at 2.4,0,0 right tc rgb "yellow" font "Times,20"
    set label 5 " U" at 0,1.5,0 left  tc rgb "green"  font "Times,20"
    set label 6 "V " at 0,0,1.7 right tc rgb "cyan"   font "Times,20"

    splot Sx(u,v),Sy(u,v),Sz(u,v) lc rgb "white", \
          Sx(u,0),Sy(u,0),Sz(u,0) lw 2 lc rgb "orange"

    unset object 1

    unset label 1
    unset label 2
    unset label 3
    unset label 4
    unset label 5
    unset label 6

    unset arrow 1
    unset arrow 2
    unset arrow 3
    unset arrow 4
    unset arrow 5

    # Plot fake x- and y-pol data

    set bmargin at screen 0.6
    set lmargin at screen 0.1
    set tmargin at screen 0.95
    set rmargin at screen 0.6

    set xrange [0:1]
    set yrange [-3:9]
    set ytics ("x" 6, "y" 0) tc rgb "white" nomirror scale 0 font ",20" offset -1
    set border 15 front lc rgb "white"
    unset parametric
    set samples 200

    plot invnorm(rand(0))     w l lc rgb "white" lw 2, \
         invnorm(rand(0))+6.0 w l lc rgb "white" lw 2

    # Plot fake IQUV data

    set bmargin at screen 0.1
    set lmargin at screen 0.1
    set tmargin at screen 0.55
    set rmargin at screen 0.6

    set xrange [0:1]
    set yrange [-3:21]
    set ytics ("I" 18, "Q" 12, "U" 6, "V" 0) tc rgb "white" nomirror scale 0 font "Times,20" offset -1
    set border 15 front lc rgb "white"
    unset parametric
    set samples 100

    plot invnorm(rand(0))      w l lc rgb "cyan"   lw 2, \
         invnorm(rand(0))+ 6.0 w l lc rgb "green"  lw 2, \
         invnorm(rand(0))+12.0 w l lc rgb "yellow" lw 2, \
         invnorm(rand(0))+18.0 w l lc rgb "white"  lw 2

    # "Draw" MWA dipole
    set bmargin at screen 0.65
    set lmargin at screen 0.7
    set tmargin at screen 0.95
    set rmargin at screen 0.9

    set xrange [-1:1]
    set yrange [-1:1]
    set zrange [-1:1]

    unset border
    unset tics

    set parametric
    set urange [-1:1]
    set vrange [-1:1]

    set label 1 "I^2 â‰¥ Q^2 + U^2 + V^2" at screen 0.8, 0.57 center font ",20" tc rgb "white"

    splot  u,  0,  0.5*u      lw 3 lc rgb "gray", \
           u,  0,  0.0*u      lw 3 lc rgb "gray", \
           u,  0, -0.5*u      lw 3 lc rgb "gray", \
           0,  u,  0.5*u      lw 3 lc rgb "gray", \
           0,  u,  0.0*u      lw 3 lc rgb "gray", \
           0,  u, -0.5*u      lw 3 lc rgb "gray", \
          -1,  0, 0.75*u-0.25 lw 3 lc rgb "gray", \
           1,  0, 0.75*u-0.25 lw 3 lc rgb "gray", \
           0,  1, 0.75*u-0.25 lw 3 lc rgb "gray", \
           0, -1, 0.75*u-0.25 lw 3 lc rgb "gray"

unset multiplot
