# Morphing a 2d grid into a 3d sphere
# Mostly the same as 2params.gpi, except that "clock" is set to 0.0
clock = 0.0

# The usual "smooth clock" function
smth_clock(t) = 3*t**2 - 2*t**3
smth_interp(val1, val2, t) = (1-smth_clock(t))*val1 + smth_clock(t)*val2

# Look and feel
set object 1 rectangle from screen 0,0 to screen 1,1 behind fc rgb "black"
unset border
unset tics
unset key
set view equal xyz
set xyplane 0
set view 90,0,1.5,1

set lt 1 lw 1 lc rgb "white"
set lt 2 lw 2 lc rgb "orange"
set lt 3 lw 2 lc rgb "red"
set lt 4 lw 1 lc rgb "yellow"

# u == phi; v == chi
phi_lo = -pi/2
phi_hi =  pi/2
chi_lo = -pi/4
chi_hi =  pi/4
set urange [-pi/2:pi/2]
set vrange [-pi/4:pi/4]

set xrange [-1:1]
set yrange [-1:1]
set zrange [-1:1]

# Parametric formulas
# ... for the initial grid
Gx(phi,chi) = 2.0 * phi / pi
Gy(phi,chi) = -1
Gz(phi,chi) = 4.0 * chi / pi

# Parametric formulas for ellipses
Px(phi,chi,phase) = cos(phi)*cos(phase) - tan(chi)*sin(phi)*sin(phase)
Py(phi,chi,phase) = sin(phi)*cos(phase) + tan(chi)*cos(phi)*sin(phase)

# Draw some custom axis labels
set label 1    "ψ" at    0,-1,-1.2 center tc rgb "white" font ",20"
set label 2    "χ" at -1.3,-1,   0 center tc rgb "white" font ",20"
set label 3   "0°" at   -1,-1,-1.1 center tc rgb "white" font ",16"
set label 4 "180°" at    1,-1,-1.1 center tc rgb "white" font ",16"
set label 5 "-45°" at -1.2,-1,  -1 center tc rgb "white" font ",16"
set label 6  "45°" at -1.2,-1,   1 center tc rgb "white" font ",16"

# Labels for R.H. vs L.H. circular polarisation
set label 7  "R.H." at 1.1,-1, 0.5 left tc rgb "white" font ",20"
set label 8  "L.H." at 1.1,-1,-0.5 left tc rgb "white" font ",20"

# Draw ellipses in every grid square
ndivs = 8
phi(x) = x*phi_hi
chi(y) = y*chi_hi

xdiff = 2.0/ndivs
xlo = -1.0 + xdiff/2.0
xhi =  1.0 - xdiff/2.0

ydiff = 2.0/ndivs
ylo = -1.0 + xdiff/2.0
yhi =  1.0 - xdiff/2.0

Xn(n) = (n-(ndivs+1)/2.0)*xdiff
Yn(n) = (n-(ndivs+1)/2.0)*ydiff

ellsize = 0.3*xdiff
phase(u) = u*2.0
ellx(x,y,t) = ellsize * Px(phi(x),chi(y),phase(t)) + x
elly(x,y,t) = -1
ellz(x,y,t) = ellsize * Py(phi(x),chi(y),phase(t)) + y

# Make the plot!
set isosamples ndivs+1
set samples 33
set parametric
splot Gx(u,v), Gy(u,v), Gz(u,v) lt 1, \
      Gx(u,0), Gy(u,0), Gz(u,0) lt 2, \
      Gx(u, pi/4), Gy(u, pi/4), Gz(u, pi/4) lt 3, \
      Gx(u,-pi/4), Gy(u,-pi/4), Gz(u,-pi/4) lt 3, \
      for [m=1:ndivs] for [n=1:ndivs] \
          ellx(Xn(m),Yn(n),u), elly(Xn(m),Yn(n),u), ellz(Xn(m),Yn(n),u) lt 4
      

unset object 1
unset for [i=1:10] label i
