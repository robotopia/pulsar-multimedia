# This script assumes that the following variables have been set
#
#  frame               (frame number: 0 <= frame <= 99)
#  Lines               (filename for magnetic field line information)
#  Traj1, Traj2, Traj3 (filenames for trajectory information)
#  P                   (pulsar spin period)
#  al                  (pulsar alpha angle)
#  ze                  (pulsar zeta angle)
#  tstep               (the time step used in Traj[123])

reset

unset key
unset border
unset tics

set xlabel "x"
set ylabel "y"
set zlabel "z"

c = 299792458
rL = c*P/(2*pi)

set xrange [-rL:rL]
set yrange [-rL:rL]
set zrange [-rL:rL]

set view equal xyz

# Functions for rotating points around the z axis
rotx(x,y,th) = x*cos(th) - y*sin(th)
roty(x,y,th) = x*sin(th) + y*cos(th)

frame_rate = 200.0              # frames per second (=Hz)
t          = frame / frame_rate # seconds
rot        = t/P * 2.0*pi       # the amount of rotation that has transpired (rad)

# Max index to plot
i1 = (floor(t / tstep) <= 37500 ? floor(t / tstep) : 0)
i2 = (floor(t / tstep) <= 37000 ? floor(t / tstep) : 0)
i3 = (floor(t / tstep) <= 38000 ? floor(t / tstep) : 0)

set title sprintf("α = %d,  ζ = %d,  P = %.1f", al, ze, P) font ",20" tc rgb "white"

splot for [l=0:359:30] Lines index l u (rotx($1,$2,rot)):(roty($1,$2,rot)):3 w l lt 1, \
                       Traj1 every ::::i1 u 2:3:4 w l lt 2 lw 2, \
                       Traj2 every ::::i2 u 2:3:4 w l lt 2 lw 2, \
                       Traj3 every ::::i3 u 2:3:4 w l lt 2 lw 2, \
                       Traj1 every ::i1::i1 u 2:3:4 w p pt 7 ps 1.5 lc rgb "green", \
                       Traj2 every ::i2::i2 u 2:3:4 w p pt 7 ps 1.5 lc rgb "green", \
                       Traj3 every ::i3::i3 u 2:3:4 w p pt 7 ps 1.5 lc rgb "green"
