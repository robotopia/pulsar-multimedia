# This script assumes a frame filename is given, e.g.
# frame = 'frame_0059.png'
# Frame numbers are expected to range from 0000 to 0959 inclusive

wth = 800
hgt = 1000

set terminal pngcairo enhanced size wth,hgt
set output frame

unset border
unset key
unset tics
unset xlabel
unset ylabel

set object 1 rectangle from screen 0,0 to screen 1,1 fc rgb "black" behind

f = frame[7:10] + 0
bf = (f+12) - floor((f+12)/48)*48 + 1
blender = sprintf("carousel/%04d.png", bf)
bh = 540.0
bw = 960.0

set multiplot

####################################################

bh_norm  = 1.0-(wth*bh)/(hgt*bw)
set lmargin at screen 0
set rmargin at screen 1
set bmargin at screen bh_norm
set tmargin at screen 1

set xrange [0:bw]
set yrange [0:bh]

plot blender binary filetype=png dx=1 dy=1 with rgbimage

unset object
unset label
unset arrow

####################################################

set lmargin at screen 0.10
set rmargin at screen 0.48
set bmargin at screen 0.10
set tmargin at screen 0.70

cblo = 0
cbhi = 10
set cbrange [cblo:cbhi] noextend

set border lw 2 lc rgb "white" front
set xrange [*:*] noextend
set palette rgbformulae 21,22,23
unset colorbox
set xtics 15 out nomirror tc rgb "white" format "%.0fÂ°"
set ytics 10 out nomirror tc rgb "white"
set xlabel "Rotation phase" tc rgb "white" font ",20"
set ylabel "Pulse number"   tc rgb "white" font ",20" offset 2

fps = 24         # frames per second
ppf = 1024.0/fps # pixels per frame
p1  = 1024*50    # starting pixel
pixel = floor(f*ppf)+p1

trail(val,p,len) = val + (cbhi-val)*(((pixel-(p) < len ? pixel-(p)-len : 0)/len)**2)
p2x(p) = (p) - floor((p)/1024)*1024    # convert pixel to x value
p2y(p) = floor((p)/1024)               # convert pixel to y value

set yrange [0+(pixel-p1)/1024.0:100+(pixel-p1)/1024.0] noextend

plot 'pulsestacks/mwa_common.dat' \
         binary array=(199,4780) \
         origin=(-2.988281e+01,0) \
         dx=3.515625e-01 \
         dy=1 \
         format='%float64' \
         u ($0 < pixel ? trail($1,$0,150.0) : cbhi) with image

####################################################

unset multiplot
