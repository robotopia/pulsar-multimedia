# Assumes "clock" is defined (0 <= clock < 1)

# Global properties
unset tics
unset border
unset key
set object 1 rectangle from screen 0,0 to screen 1,1 behind fc rgb "black"

set multiplot

# The rotating pulsar

set bmargin at screen 0.15
set lmargin at screen 0.01
set tmargin at screen 0.99
set rmargin at screen 0.98

unset xlabel
unset ylabel

plot "0001.png" binary filetype=png with rgbimage

unset object 1

# The pulsestack

set palette rgbformulae 21,22,23
unset colorbox

set autoscale noextend

set bmargin at screen 0.07
set lmargin at screen 0.60
set tmargin at screen 0.70
set rmargin at screen 0.95

set xlabel "Pulse Rotation" font ",18" tc rgb "white"

set arrow 1 from -130,-2.7 to -180,-2.7 lc rgb "white" lw 2
set arrow 2 from  130,-2.7 to  180,-2.7 lc rgb "white" lw 2

plot 'pulsestack.bin' binary array=(1024,75) origin=(-180,1) dx=.35156250000000000000 dy=1 format='%float64' with image notitle

unset multiplot
