reset

unset border
unset tics

set view equal xyz
set view 69,289,1.4

set arrow 1 from -2, 0, 0 to 2.5,  0,0   dt 3 nohead
set arrow 2 from  0,-1, 0 to   0,2.7,0   dt 3 nohead
set arrow 3 from  0, 0,-1 to   0,  0,2.0 dt 3 nohead

set arrow 4 from 0,0,0 to 1, 0, 1.5 lw 2 front
set arrow 5 from 0,0,0 to 0, 2, 0   lw 2 front
set arrow 6 from 0,0,0 to 1, 2, 1.5 lw 2 front

set arrow  7 from 1, 0, 0 to 1, 0, 1.5 dt 2 nohead
set arrow  8 from 1, 2, 0 to 1, 2, 1.5 dt 2 nohead
set arrow  9 from 0, 2, 0 to 1, 2, 0   dt 2 nohead
set arrow 10 from 0, 0, 0 to 1, 2, 0   dt 2 nohead
set arrow 11 from 0, 2, 0 to 1, 2, 1.5 dt 2 nohead

set label 1 "Γ"       at 0.90, 1.80, 1.10 center tc rgb "blue" font ",16"
set label 2 "Γ_{dip}" at 0.95, 2.00, 1.00 center tc rgb "blue" font ",12"
set label 3 "Δφ"      at 0.50, 1.80, 0.00 center tc rgb "blue" font ",16"
set label 4 "Γ"       at 0.10, 0.20, 0.40 center tc rgb "blue" font ",16"
set label 5 "Γ_{dip}" at 0.05, 0.00, 0.50 center tc rgb "blue" font ",12"

set label 6 "V"   at  0.50,  1.0, 0.90 center font ",16"
set label 7 "V_B" at  0.50, -0.1, 0.75 center font ",16"
set label 8 "V_φ" at -0.15,  1.0, 0.00 center font ",16"

set label  9 "x" at  2.5, -0.1, 0.0 center font "Times,14"
set label 10 "y" at -0.1,  2.7, 0.0 center font "Times,14"
set label 11 "z" at  0.0,  0.0, 2.1 center font "Times,14"

set xrange [-1:2]
set yrange [-0.5:2.5]
set zrange [-1:2]

R = 5
G = atan(1.5/1)

th = acos(1.0/3.0*(cos(G)*sqrt(8+cos(G)**2) - sin(G)**2))/2.0
r  = R*sin(th)**2
X  = r*sin(th)
Z  = r*cos(th)

set parametric

set urange [0:1]
set vrange [0:1]

set key font ",16"

set object 1 circle at 1,2,1.5 size 0.3 arc [270:309]
set object 2 circle at 1,2,1.5 size 0.4 arc [260:270]
set object 3 circle at 1,2,0   size 0.1 arc [227:340]
set object 4 circle at 0,0,0   size 0.3 arc [ 90:129]
set object 5 circle at 0,0,0   size 0.4 arc [ 80: 90]

splot 3*u-1, 3*v-0.5, 0 lc rgb "gray" notitle, \
      R*sin(u*pi/2)**3 - X, 0, R*sin(u*pi/2)**2*cos(u*pi/2) - Z lc rgb "red" title "Magnetic field line"
