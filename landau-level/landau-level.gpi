# This script expects "frame" to be defined (0 <= frame < 50)
reset

T = frame / 50.0

set samples 500

unset border
unset tics
set parametric
set view equal xy
set xyplane 0
unset key

set urange [0:1]
set vrange [0:1]
set xrange [-1.5:1]
set yrange [-1:1]
set zrange [0:0.85]

mr(z) = 0.4*exp(-z/0.15) # "maximum spiral radius"
pa = 0.01 # "pitch angle"

spiralx(t) = mr(t)*cos(t/pa)-1+sqrt(1-t**2)
spiraly(t) = mr(t)*sin(t/pa)
spiralz(t) = (t < T ? t : 1/0)

set view 60,30,1.3,1

set multiplot

set object 1 rectangle from screen 0,0 to screen 1,1 fc rgb "black" behind

splot for [i=-1:1] for [j=-1:1] cos(u)+i-1,j,sin(u) lc rgb "#ff00dd", \
      spiralx(u),spiraly(u),spiralz(u)              lc rgb "#ff6000", \
      u*2-1,v*2-1,0 lc rgb "gray"

unset object
set samples 2
set isosamples 2

splot spiralx(T),spiraly(T),spiralz(T-1e-10) w p pt 7 ps 0.6 lc rgb "yellow", \
      spiralx(T),spiraly(T),0                w p pt 7 ps 0.6 lc rgb "gray"

unset multiplot
