set terminal pngcairo enhanced size 960,400 background rgb "black"

set style line 1 lw 1 lc rgb "white"

P0 = 0.9429509945598
npulse = 100
T = P0 * npulse
framerate = 24.0
lastframe = ceil(T*framerate)

gettime(pulseno, phasebinno) = P0 * (pulseno + phasebinno/1024.0)

do for [frame=0:lastframe] {

    t = frame/framerate
    windowlength = T/4.0
    window = floor(t/windowlength)

    x1 = window*windowlength
    x2 = (window + 1)*windowlength

    y1 = -0.9
    y2 = 10.4

    set xrange [x1:x2]
    set yrange [y1:y2]

    outfile = sprintf("timeseries_%04d.png", frame)
    set out outfile

    print "Plotting frame ".frame
    plot 'data_short.txt' u (gettime($1, $3) < t ? gettime($1, $3) : 1/0):4 w l ls 1 notitle

}
