# A noisy pulse train

load "common.gpi"

rands      = system("cat rands.txt")

pulse(n,x)         = f(n,pulsewidth,x)
pulsetrain(n,x)    = (n==0) ? \
                       pulse(n-0.5,x)*word(rands,n) : \
                       pulse(n-0.5,x)*word(rands,n) + pulsetrain(n-1,x)
noisytrain(n,x)    = pulsetrain(n,x) + invnorm(rand(0)) * noiselevel

nsamps = 2000
set samples nsamps

set print $pulses
do for [i=1:nsamps] {
    X = (i-1.0)*npulses/nsamps
    print sprintf("%f",noisytrain(npulses,X))
}

set print $repeated
print $pulses
print $pulses

set table
plot $repeated u ($0*npulses*1.0/nsamps):1 with table

